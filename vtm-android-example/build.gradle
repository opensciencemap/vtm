apply plugin: 'com.android.application'

repositories {
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

dependencies {
    implementation project(':vtm-android')
    implementation project(':vtm-http')
    implementation(project(':vtm-jeo')) {
        exclude group: 'com.vividsolutions', module: 'jts'
    }
    implementation project(':vtm-json')
    implementation project(':vtm-jts')
    implementation project(':vtm-themes')
    implementation 'com.noveogroup.android:android-logger:1.3.6'
    implementation 'com.vividsolutions:jts-core:1.14.0'
    implementation('org.jeo:jeo:0-SNAPSHOT') {
        exclude group: 'com.vividsolutions', module: 'jts'
        exclude group: 'org.slf4j', module: 'slf4j-jdk14'
    }
    implementation('org.jeo:jeo-render:0-SNAPSHOT') {
        exclude group: 'com.vividsolutions', module: 'jts'
        exclude group: 'org.slf4j', module: 'slf4j-jdk14'
    }
    implementation 'com.android.support:support-v4:27.0.0'

    implementation 'org.mapsforge:mapsforge-core:master-SNAPSHOT'
    implementation 'org.mapsforge:mapsforge-poi:master-SNAPSHOT'
    implementation 'org.mapsforge:mapsforge-poi-android:master-SNAPSHOT'
    implementation 'org.mapsforge:spatialite-android:master-SNAPSHOT'
    implementation 'org.mapsforge:spatialite-android:master-SNAPSHOT:natives-armeabi'
    implementation 'org.mapsforge:spatialite-android:master-SNAPSHOT:natives-armeabi-v7a'
    implementation 'org.mapsforge:spatialite-android:master-SNAPSHOT:natives-x86'
}

android {
    compileSdkVersion androidCompileSdk()
    buildToolsVersion "$androidBuildVersionTools"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    defaultConfig {
        versionCode versionCode()
        versionName versionName()
        minSdkVersion androidMinSdk()
        targetSdkVersion androidTargetSdk()
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
            file("${rootDir}/vtm-android/natives").eachDir() { dir ->
                jniLibs.srcDirs += "${dir.path}/lib"
            }
        }
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }

    lintOptions { abortOnError false }

    packagingOptions {
        exclude 'META-INF/services/org.jeo.data.Driver'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }
}

task run(dependsOn: 'installDebug') {
    doFirst {
        println(">> adb run...")
        String adb = System.getenv()['ANDROID_HOME'] + '/platform-tools/adb'
        String cmd = "${adb} shell am start -n org.oscim.android.test/.Samples"
        def proc = cmd.execute()
        proc.in.eachLine { line -> println line }
        proc.err.eachLine { line -> System.err.println('ERROR: ' + line) }
        proc.waitFor()
    }
}
